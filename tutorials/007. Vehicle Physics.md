Vehicle Physics
===============
It is perfectly possible to do car physics with dmech. The most common and time-proven approach for real-time car simulation is ray cast vehicle model, and dmech provides all necessary means to implement it.

Method Overview
---------------
At first glance, it seems straightforward to make a car with rigid body wheels and constraints. Why not do that? Well, it is possible in principle but has a few disadvantages: we would need more complex constraints to simulate suspension. The sliding behavior of wheels is different from standard rigid bodies, because real wheels aren't rigid bodies. The system would be far too complicated and unstable. 

Cars and other vehicles are complex mechanical devices and most often we do not want to simulate that, only that it acts as expected. Ray cast model allows us to do that without having to simulate all the physics that would actually happen with the real car.

The key idea is to represent wheels and suspension as imaginary objects that affect the car with forces. These forces keep the chassis 'floating' above the ground as if it was resting on wheels. We can calculate these forces using ray casting and a simple spring-damper system. Tyre friction is also not hard to simulate.

Main advantages of ray cast approach is stability, high performance, realistic behaviour and a simple interface with very few parameters. The only disadvantage is absense of wheel geometry, and, as a consequence, approximated collision detection between wheels and environment.

Wheel
-----
It is convenient to define a wheel class and then create necessary number of wheel objects in the chassis:
```d
class Wheel
{
    // Wheel parameters
    float radius; // radius of a wheel
    float direction; // -1.0 for left wheel, 1.0 for right wheel - useful for transformations
    float sideFrictionCoefficient; // 0.6..0.9 depending on the material of the road and tyre protectors

    // Suspension parameters
    Vector3f suspensionPosition; // point where suspension is attached to chassis, defined in chassis local space
    float suspensionMaxLength; // uncompressed spring length
    float suspensionStiffness; // spring rigidity
    float suspensionDamping; // spring oscillation decay

    // Suspension state
    float suspensionLength; // current length of a spring
    float suspensionLengthPrev; // length of a spring on a previous integration step
    float suspensionCompression; // suspensionMaxLength - suspensionLength

    // Wheel state
    Vector3f position;
    Matrix4x4f transformation;
    Vector3f forcePosition;
    float torque;
}
```

Chassis
-------
A chassis is an object that represents our car. It encapsulates a rigid body and a number of wheels.
```d
class Chassis
{
    RigidBody chassisBody;
    Wheel[4] wheels;
}
```

Suspension
----------
To calculate suspension force for a wheel, we start by casting a ray for each wheel downwards along its suspension axis. Take care of transformation operations to do proper space conversions - ray casting in dmech works in world space.
```d
Vector3f wheelPosWorld = chassisBody.position + chassisBody.orientation.rotate(wheel.suspensionPosition);
Vector3f suspensionAxisDown = -wheel.transformation.up; // a 'down' vector of a wheel in world space

CastResult castResult;
float groundHeight = 0.0f;
Vector3f groundNormal = Vector3f(0.0f, 1.0f, 0.0f);
if (world.raycast(wheelPosWorld, suspensionAxisDown, 10.0f, castResult, true, true))
{
    groundHeight = castResult.point.y;
    groundNormal = castResult.normal;
}
```
Using the ray's intersection point, we can calculate the suspension length and hence the suspension force that is then applied to the chassis, keeping it from hitting the ground. 

First we calculate suspension length. We should distinguish two cases: when the wheel collides with the ground (suspension is compressed), and when it is in air (suspension released). 
```d
float suspensionToGround = wheelPosWorld.y - groundHeight;
wheel.forcePosition = Vector3f(wheelPosWorld.x, groundHeight, wheelPosWorld.z);

if (suspensionToGround > (wheel.suspensionMaxLength + wheel.radius)) // wheel is in air
{
    wheel.suspensionCompression = 0.0f;
    wheel.suspensionLengthPrev = wheel.suspensionMaxLength;
    wheel.suspensionLength = wheel.suspensionMaxLength;
    wheel.position = wheel.suspensionPosition + Vector3f(0.0f, -wheel.suspensionMaxLength, 0.0f);
}
else // suspension is compressed
{
    wheel.suspensionLengthPrev = wheel.suspensionLength;
    wheel.suspensionLength = suspensionToGround - wheel.radius;
    wheel.suspCompression = wheel.suspensionMaxLength - wheel.suspensionLength;
    wheel.position = w.suspensionPosition + Vector3f(0.0f, -w.suspensionLength, 0.0f);

    applyWheelForce(wheel, deltaTime);
}
```

Normal Force
------------
In `applyWheelForce` method, we calculate a normal force - a force that pushes chassis upwards along suspension axis:
```d
float springForce = wheel.suspCompression * wheel.suspensionStiffness;
float dampingForce = ((wheel.suspensionLengthPrev - wheel.suspensionLength) * wheel.suspensionDamping) / deltaTime;
float normalForce = springForce + dampingForce;
```

This force is then applied to the chassis at the point where wheel collides with the ground - `wheel.forcePosition` that we calculated earlier:
```d
Vector3f suspensionAxisUp = wheel.transformation.up; // an 'up' vector of a wheel in world space
Vector3f normalForceVec = suspensionAxisUp * normalForce;
chassisBody.applyForceAtPos(normalForceVec, wheel.forcePosition);
```

Forward Force
--------------
To make our car move, an engine force should be applied to one or more wheels in the forward direction. It is called forward force and is proportional to wheel torque that it receives from the gearbox. Wheels can rotate clockwise or counter-clockwise, and thus the car can move forward and backward.
```d
float forwardForce = wheel.torque / wheel.radius;
Vector3f forwardDir = wheel.transformation.forward;
chassisBody.applyForceAtPos(forwardDir * forwardForce, wheel.forcePosition);
```

Side Force
----------
There is a third force that is acting to the chassis - a side force (or lateral force) due to tyre friction. It allows the car to steer by preventing it to slide in opposite direction to turning circle. Side force is proportional to wheel load (normal force) and side velocity of a wheel. It is adjusted by friction coefficient. The formula used below is not physically accurate due to non-linear nature of tyre slip, but it works and actually can be convincing for low-speed driving.
```d
Vector3f radiusVector = wheel.forcePosition - chassisBody.position;
Vector3f pointVelocity = chassisBody.linearVelocity + cross(chassisBody.angularVelocity, radiusVector);
Vector3f sideDir = wheel.transformation.right * wheel.direction;
float sideSpeed = dot(pointVelocity, sideDir);
float sideForce = sideSpeed * normalForce * wheel.sideFrictionCoefficient;
chassisBody.applyForceAtPos(-sideDir * sideForce, wheel.forcePosition);
```

Conclusion
----------
Writing realistic and fun to play driving simulator is a huge of a challenge. This tutorial gives only a basic idea of its core concepts. There is much more to do: chassis class must provide an intuitive API to control the car, there should be some glue code to synchronize graphical representation of wheels with their physical state, and simulator itself is a vast area for improvements and fine-tuning. There is a lot of things that we haven't covered here - such as steering, transmission, dynamic weight transfer. We hope you now ready to do some study and implement these features yourself, improving our simple vehicle model to something cool. Good luck with your physics-based games!
